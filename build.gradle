plugins {
    id 'java'
    id 'io.jmix' version '2.7.4'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
}

apply plugin: 'com.vaadin'
apply plugin: 'org.springframework.boot'

group = 'com.company'
version = '0.0.1-SNAPSHOT'

jmix {
    bomVersion = '2.7.4'
}

vaadin {
    optimizeBundle = false
}

bootJar {
    archiveFileName = "crm.jar"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()

    maven {
        url = 'https://nexus.jmix.io/repository/public'
    }

    maven {
        url = 'https://maven.vaadin.com/vaadin-addons'
    }
}

dependencies {
    //              --- [ JMIX CORE ] ---
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'

    implementation 'io.jmix.security:jmix-security-starter'
    implementation 'io.jmix.security:jmix-security-flowui-starter'
    implementation 'io.jmix.security:jmix-security-data-starter'

    implementation 'io.jmix.flowui:jmix-flowui-starter'
    implementation 'io.jmix.flowui:jmix-flowui-data-starter'
    implementation 'io.jmix.flowui:jmix-flowui-themes'

    //              --- [ JMIX ADD-ONS ] ---
    implementation 'io.jmix.audit:jmix-audit-starter'
    implementation 'io.jmix.audit:jmix-audit-flowui-starter'

    implementation 'io.jmix.appsettings:jmix-appsettings-starter'
    implementation 'io.jmix.appsettings:jmix-appsettings-flowui-starter'

    implementation 'io.jmix.datatools:jmix-datatools-starter'
    implementation 'io.jmix.datatools:jmix-datatools-flowui-starter'

    implementation 'io.jmix.dynattr:jmix-dynattr-starter'
    implementation 'io.jmix.dynattr:jmix-dynattr-flowui-starter'

    implementation 'io.jmix.reports:jmix-reports-rest-starter'
    implementation 'io.jmix.reports:jmix-reports-starter'
    implementation 'io.jmix.reports:jmix-reports-flowui-starter'

    implementation 'io.jmix.charts:jmix-charts-flowui-starter'

    implementation 'io.jmix.gridexport:jmix-gridexport-flowui-starter'

    implementation 'io.jmix.localfs:jmix-localfs-starter'

    implementation 'io.jmix.jmxconsole:jmix-jmxconsole-starter'

    //              --- [ VAADIN ADD-ONS ] ---
    implementation 'org.vaadin.jchristophe:sortable-layout:2.1.0'

    //              --- [ SPRING ] ---
    implementation 'org.springframework.boot:spring-boot-starter-web'

    //              --- [ DATABASE ] ---
    runtimeOnly 'org.hsqldb:hsqldb'
    // For online mode
    implementation 'org.apache.commons:commons-dbcp2'

    //              --- [ OTHER ] ---
    // Java Faker for demo data generation
    implementation 'net.datafaker:datafaker:2.5.0'

    //              --- [ TESTS ] ---
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.jmix.flowui:jmix-flowui-test-assist'
}

configurations.implementation {
    exclude group: 'com.vaadin', module: 'hilla'
    exclude group: 'com.vaadin', module: 'hilla-dev'
    exclude group: 'com.vaadin', module: 'copilot'
    exclude group: 'commons-logging', module: 'commons-logging'
}

tasks.register("testPreparing", Delete) {
    dependsOn(cleanConf)
    doLast {
        def dbDir = file('.jmix/hsqldb')
        if (dbDir.exists()) {
            dbDir.listFiles().each { file ->
                if (file.name.startsWith('test.')) {
                    file.delete()
                    println "Deleted: ${file.name}"
                }
            }
        }
    }
}

test {
    dependsOn(testPreparing)
    useJUnitPlatform()
}

idea {
    module {
        excludeDirs.addAll(files '.jmix',
                'node_modules',
                'src/main/frontend/generated/',
                'src/main/bundles')
    }
}

bootBuildImage {
    if (rootProject.hasProperty('jmixDockerTag')) {
        imageName = rootProject['jmixDockerTag']
    }
}
